#include "libn3d.hsp"

*init
	screen 0
	n3d_initialize
	n3dm_initialize
	
	sdim testplane,65536
	bload "testplane.n3dm",testplane
	sdim laser,65536
	bload "laser.n3dm",laser
	
	n3d_setCameraPosition -48.0, 0.0, -24.0
	n3d_setCameraAngle 0.0, pi*0.3,0.0
	
	x=0.0
	y=0.0

	dim bul_avail,64
	dim bul_x,64
	dim bul_y,64
	
*main
	repeat
		redraw 0
		color 0,0,0
		boxf
		color 255,255,255
		n3d_deleteall
		
		stick ky,15+2048
		if ky&1{
			x-=1.6
			xr=0.3
		}
		if ky&2{
			y-=1.6
			yr=0.2
		}
		if ky&4{
			x+=1.6
			xr=-0.3
		}
		if ky&8{
			y+=1.6
			yr=-0.2
		}
		if (ky&1)/1 = (ky&4)/4{
			xr=0.0
		}
		if (ky&2)/2 = (ky&8)/8{
			yr=0.0
		}
		if (ky&2048)=2048 & cnt\4=0{
			repeat 64
				if bul_avail(cnt)=0{
					bul_avail(cnt)=1
					bul_x(cnt)=x-5
					bul_y(cnt)=y+15
					break
				}
			loop
			repeat 64
				if bul_avail(cnt)=0{
					bul_avail(cnt)=1
					bul_x(cnt)=x+5
					bul_y(cnt)=y+15
					break
				}
			loop
		}
		n3dm_setModel testplane, 1, -y, -x, 0.0
		n3d_rotateXByGroup 1, -y, -x, 0.0, xr
		n3d_rotateYByGroup 1, -y, -x, 0.0, yr
		
		n3d_deleteByGroup 2
		repeat 64
			if bul_avail(cnt)=1{
				bul_y(cnt)-=15
				n3dm_setModel laser, 100+cnt, -bul_y(cnt),-bul_x(cnt)-0.2, 0.0
				if bul_y(cnt)<-700{
					bul_avail(cnt)=0
				}
			}
		loop
		
		n3d_render
		n3d_renderinfo
		
		redraw 1
		await 33+cnt\3/2
	loop
