#include "mod_joystick2.as"
#include "libn3d.hsp"
#include "mod_taggedlabel.hsp"
#define FULLSCREEN 0

#define JOYSTICK_PORT 0

#define KY_LEFT		((ky&    1)>> 0)
#define KY_UP 		((ky&    2)>> 1)
#define KY_RIGHT	((ky&    4)>> 2)
#define KY_DOWN		((ky&    8)>> 3)
#define KY_Z		((ky& 2048)>>11)


*init
	screen 0, 800, 800, SCREEN_HIDE
	if FULLSCREEN = 0{
		screen 1, 640, 480
	}
	if FULLSCREEN = 1{
		screen 1, 640, 480, SCREEN_HIDE
		scrzoom = limitf(double(GINFO_DISPX)/640, 0.0, double(GINFO_DISPY)/480)
		bgscr 29, GINFO_DISPX, GINFO_DISPY, 0, 0, 0
		cls 4
	}
	
	sdim testplane,65536
	bload "testplane.n3dm",testplane
	sdim laser,65536
	bload "laser.n3dm",laser
	sdim whitebox, 65536
	bload "whitebox.n3dm", whitebox
	
	//背景星
	screen 2, 1600, 600, SCREEN_HIDE
	cls 4
	repeat 256
		x = rnd(1600)
		y = rnd(400)
		s = rnd(2)+1
		rgbcolor $888888
		circle x-s, y-s, x+s, y+s, 1
	loop
*start
	n3d_initialize
	n3dm_initialize
	
	taglab_init
//	n3d_setCameraPosition -24.0, 0.0, -24.0
//	n3d_setCameraAngle 0.0, pi*0.35,0.0
	gsel 0
	
	//n3d_setCameraPosition -48.0, 0.0, -32.0
	//n3d_setCameraAngle 0.0, pi*0.25,0.0
	
	x=-195.2
	y=0.0
	z = -8.0
	ddim roll
	roll = 0.0
	ddim pitch
	pitch = 0.0
	ddim roll_targ
	roll_targ = 0.0
	ddim pitch_targ
	pitch_targ = 0.0
	
	
	dim bul_avail, 64
	dim bul_x, 64
	dim bul_y, 64
	dim bul_z, 64
	dim reshot
	
	dim cammode
	cammode = -1
	dim camtime
	camtime = 0
	
	dim  enem_avail	 	, 32
	ddim enem_x			, 32
	ddim enem_y			, 32
	sdim enem_model,4096, 32
	
	dim fa, 128
	ddim fx, 128
	ddim fy, 128
	ddim fz, 128
	ddim fxs, 128
	ddim fys, 128
	ddim fzs, 128
	ddim ft, 128
	ddim fp, 128
	ddim fts, 128
	ddim fps, 128
	ddim fs, 128
	dim flt, 128
	
	dim airplane_speed
	airplane_speed = 6
	
	dim airplane_manuever_avail
	airplane_manuever_avail = 0
	
	dim axis_progress
	axis_progress = 0
	
	dim game_cameramode
	game_cameramode = 0
	
	dim game_reset
	game_reset = 0
	
*main
	repeat
		gsel 0
		redraw 0
		color 0,0,0
		boxf
		color 255,255,255
		
		
		n3d_deleteall
		
		gosub *proc_key
		
		if cammode = -1{
			//camtime
			gosub *proc_axis
			gosub *proc_title_viewship
			gosub *proc_titletext
			gosub *title_control
			n3d_render
		}else:if cammode >= 0{
			gosub *proc_camera
			gosub *proc_background
			gosub *proc_axis
			gosub *proc_enemy
			gosub *proc_reticle
			gosub *proc_starship
			gosub *proc_bullet
			gosub *proc_f
			n3d_render
		}
		
		camtime += 1
		
		redraw 1
		
		gsel 1
		redraw 0
		gmode 0, 800, 800
		pos 320, 240
		grotate 0, 0, 0, deg2rad(roll / 4), 800, 800
		n3d_renderinfo
		rgbcolor $EEEEEE
		taglab_draw
		redraw 1
		
		if FULLSCREEN = 1{
			gsel 29
			pos (GINFO_DISPX - scrzoom * 640) / 2, (GINFO_DISPY - scrzoom * 480) / 2
			gzoom scrzoom * 640, scrzoom * 480, 1, 0, 0, 640, 480, 0
		}
		
		await 50
		if game_reset = 1{
			break
		}
		if ky & 128{
			game_reset = 0
			break
		}
//		if camtime=60:stop
	loop

	if game_reset = 1{
		goto *start
	}
	
	end
stop





*proc_key
	stick ky,15+2048
	joyGetPosEx jky, JOYSTICK_PORT
	if stat = 0{
		dialog "Joystick Error"
		end
	}
	return

*proc_starship
	if airplane_manuever_avail = 1{
		kya = ky & (1 + 2 + 4 + 8 + 2048)
	}else{
		if cammode = 1 and camtime >= 40{
			kya = 4
		}else:if cammode = 3 and axis_progress = 0{
			kya = 2
		}else{
			kya = 0
		}
	}
	if kya&1{
		x-=3.2
		roll_targ = 40
		//xr=0.3
	}
	if kya&2{
		//y-=1.6
		z -= 3.2
		//yr=0.2
		pitch_targ = -20
	}
	if kya&4{
		x+=3.2
		//xr=-0.3
		roll_targ = -40
	}
	if kya&8{
		//y+=1.6
		z += 3.2
		pitch_targ = 20
		//yr=-0.2
	}
	if (kya&1)/1 = (kya&4)/4{
		//xr=0.0
		roll_targ = 0.0
	}
	if (kya&2)/2 = (kya&8)/8{
		//yr=0.0
		pitch_targ = 0.0
	}
	
	if       abs(pitch_targ - pitch) <= 10{
		pitch = pitch_targ
	}else:if pitch_targ > pitch{
		pitch += 5
	}else{
		pitch -= 5
	}
	
	if       abs(roll_targ - roll) <= 5{
		roll = roll_targ
	}else:if roll_targ > roll{
		roll += 10
	}else{
		roll -= 10
	}
	
	if (kya&2048)=2048 & reshot=0{
		reshot = 3
		repeat 64
			if bul_avail(cnt)=0{
				bul_avail(cnt)=1
				bul_x(cnt)= x - 5.0 * cos(deg2rad(roll))
				bul_y(cnt)=/*y+*/15+rnd(4)
				bul_z(cnt) = z + 5.0 * sin(deg2rad(roll))
				break
			}
		loop
		repeat 64
			if bul_avail(cnt)=0{
				bul_avail(cnt)=1
				bul_x(cnt)= x + 5.0 * cos(deg2rad(roll))
				bul_y(cnt)=/*y+*/15+rnd(4)
				bul_z(cnt) = z - 5.0 * sin(deg2rad(roll))
				break
			}
		loop
	}
	reshot = limit(reshot-1, 0, 16)
	
	if axis_progress = 0 and cammode = 3{
		y -= airplane_speed * 2
		//z -= 2.0
	}else{
		z = limitf(z, -100.0, 0.0)
	}
	
	
	n3dm_setModel testplane, 1, -y, -x, z
	n3d_rotateXByGroup 1, -y, -x, z,  double(roll) * 2 * PI / 360//xr
	n3d_rotateYByGroup 1, -y, -x, z, double(pitch) * 2 * PI / 360//yr
	return


*proc_bullet
	n3d_deleteByGroup 2
	repeat 64
		if bul_avail(cnt)=1{
			bul_y(cnt)-=24
			n3dm_setModel laser, 100+cnt, -bul_y(cnt),-bul_x(cnt)-0.2, bul_z(cnt)
			if bul_y(cnt)<-480{
				bul_avail(cnt)=0
			}
		}
	loop
	
	
	return

*proc_camera
	if cammode = 1{
		if camtime <= 1{
			axis_progress = 1
		}
		if camtime < 40{
			sectime = camtime
			n3d_setCameraPosition 116.0 - 2.5 * sectime, 0.0 - x, 2.0 + z
			//n3d_setCameraAngle pi, pi*0.5, 0.0
			n3d_setCameraAngleByLookingAt 0.0, -x, z
		}
		if camtime >= 40 and camtime <= 40 + 60{
			repeat 3
				enem_avail(cnt) = 1
				enem_x(cnt) = 32.0 * cnt - 32.0
				enem_y(cnt) = 512.0
				enem_z(cnt) = -5.0 + abs(cnt - 1) * -20.0
				enem_model(cnt) = whitebox
			loop
			sectime = camtime - 40
			n3d_setCameraPosition 32.0 * cos(deg2rad(sectime * 3))- 16.0, 32.0 * sin(deg2rad(sectime * 3)) + 0.0 - x, 2.0 - (sectime) * 10 / 60 + z
			n3d_setCameraAngleByLookingAt 0.0, -x, z
		}
		if camtime = 100{
			cammode = 0
			airplane_manuever_avail = 1
		}
	}
	//camtime += 1
	if cammode = 0{
		if game_cameramode = 0{
			n3d_setCameraPosition -48.0, 0.0 - x, -8.0 + z
			n3d_setCameraAngleByLookingAt 0.0, -x, z
		}
		if game_cameramode = 1{
			n3d_setCameraPosition 0.0, 0.0 - x, -4.0 + z
			n3d_setCameraAngle 0.0, pi/2, 0.0
		}
	}
	if cammode = 2{
		sectime = camtime
		if camtime <= 30{
			n3d_setCameraPosition 48.0 * cos(deg2rad(180 - sectime * 5))- 0.0, 32.0 * sin(deg2rad(180 - sectime * 5)) + 0.0 - x, -8.0 + z
			n3d_setCameraAngleByLookingAt 0.0, -x, z
		}else{
			sectime = 30
			n3d_setCameraPosition 48.0 * cos(deg2rad(180 - sectime * 5))- 0.0, 32.0 * sin(deg2rad(180 - sectime * 5)) + 0.0 - x, -8.0 + z
			n3d_setCameraAngleByLookingAt 0.0, -x, z
		}
		if camtime = 90{
			cammode = 3
			camtime = 0
			airplane_manuever_avail = 0
		}
	}
	if cammode = 3{
		axis_progress = 0
		n3d_setCameraAngleByLookingAt -y, -x, z
	}
	t = c_sightt@nastty3d
	t = t + deg2rad(double(-pitch) / 5)
	c_sightt@nastty3d = t
	
	if ky&64{
		game_cameramode = (game_cameramode + 1) \ 2
	}
	
	if ky&1024{
		game_reset = 1
		//cammode = 2
		//camtime = 0
	}
	
	return

*proc_axis
	//Y(縦線
	repeat 51
		lcnt = cnt - 25 - ( x ) / 16
		if game_cameramode = 1 and cammode = 0{
			n3d_setpolygon 256, $444444, -24.0, 16.0 * lcnt, 0.0, 	768.0, 16.0 * lcnt, 0.0, 	-24.0, 16.0 * lcnt, 0.0
		}else{
			n3d_setpolygon 256, $444444, 0.0, 16.0 * lcnt, 0.0, 	768.0, 16.0 * lcnt, 0.0, 	-48.0, 16.0 * lcnt, 0.0
		}
		n3d_setpolygon 256, $444444, 32.0, 16.0 * lcnt, 0.0, 	256.0, 16.0 * lcnt, 0.0, 	128.0, 16.0 * lcnt, 0.0
		//n3d_setpolygon 256, $444444, 64.0, 16.0 * lcnt, 0.0,  8.0, 16.0 * lcnt, 0.0,  0.0, 16.0 * lcnt, 0.0
		n3d_setpolygon 256, $444444, 16.0, 16.0 * lcnt, 0.0, 36.0, 16.0 * lcnt, 0.0, -512.0, 16.0 * lcnt, 0.0
	loop
	if axis_progress = 1{
		axistimer = (axistimer + airplane_speed) \ 16
	}
	//X(横線
	repeat 96
		lcnt = cnt - 40
		n3d_setpolygon 256, $444444, 16.0 * lcnt - axistimer, -384.0 - x, 0.0,  16.0 * lcnt - axistimer, 384.0 - x, 0.0,  16.0 * lcnt - axistimer, 0.0 - x, 0.0
	loop
	return

*proc_enemy
	repeat length(enem_avail)
		if enem_avail(cnt) = 1{
			ecnt = cnt
			repeat length(bul_avail)
				if bul_avail(cnt) = 1{
					if absf(-bul_x(cnt) - enem_x(ecnt)) < 10.0 and absf(-bul_y(cnt) - enem_y(ecnt)) < 26.0 and absf(bul_z(cnt) - enem_z(ecnt)) < 10.0 and enem_avail(ecnt) = 1{
						logmes ecnt
						enem_avail(ecnt) = 0
						bul_avail(cnt) = 0
						repeat 8
							gosub *function_f_findvacant
							fx(t) = enem_y(ecnt)
							fy(t) = enem_x(ecnt) + double(rnd(21) - 10)/2
							fz(t) = enem_z(ecnt) + double(rnd(21) - 10)/2
							ft(t) = double(rnd(157)) / 100
							fp(t) = double(rnd(157)) / 100
							fa(t) = 1
							fs(t) = rnd(6) + 6
							fxs(t) = double(rnd(21) - 10)/10
							fys(t) = double(rnd(21) - 10)/10
							fzs(t) = double(rnd(21) - 10)/10
							fts(t) = double(rnd(11) - 5) / 100
							fps(t) = double(rnd(11) - 5) / 100
							flt(t) = 0
						loop
					}
				}
			loop
		}
	loop
	
	repeat length(enem_avail)
		if enem_avail(cnt) = 1{
			n3dm_setModel enem_model(cnt), 512 + cnt, enem_y(cnt), enem_x(cnt), enem_z(cnt)
			enem_y(cnt) -= airplane_speed
		}
	loop
	
	return

*proc_f
	repeat length(fa)
		if fa(cnt) = 1{
			fx(cnt) += fxs(cnt)
			fy(cnt) += fys(cnt)
			fz(cnt) += fzs(cnt)
			ft(cnt) += fts(cnt)
			fp(cnt) += fps(cnt)
			
			flt(cnt) += 1
			
			if axis_progress = 1{
				fx(cnt) -= airplane_speed
			}
			
			//n3d_deleteByGroup 1024 + cnt
			
			n3d_setpolygon 1024 + cnt, $FFFFFF, fx(cnt), fy(cnt), fz(cnt),  fx(cnt) - fs(cnt), fy(cnt), fz(cnt),  fx(cnt), fy(cnt) - sqrt(fs(cnt) * 3 / 2), fz(cnt) - sqrt(fs(cnt) * 3 / 2)
			n3d_rotateXByGroup 1024 + cnt, fx(cnt), fy(cnt), fz(cnt),ft(cnt)
			n3d_rotateYByGroup 1024 + cnt, fx(cnt), fy(cnt), fz(cnt),fp(cnt)
			if flt(cnt) \ 10 = 9{
				fs(cnt) -= 1
				if fs(cnt) = 0{
					fa(cnt) = 0
				}
			}
			if flt(cnt) > 128{
				fa(cnt) = 0
			}
		}else{
		}
		
	loop//*/
	
	return

*function_f_findvacant
	t = (t + 1) \ length(fa)
	repeat length(fa)
		if fa(cnt) = 0{
			t = cnt
			break
		}
	loop
	return


*proc_background
	gmode 0
	pos -800 + rad2deg(c_sightp@nastty3d) * 1600 / 360, -pitch
	gcopy 2, 0, 0, 1600, 600
	pos -1600-800 + rad2deg(c_sightp@nastty3d) * 1600 / 360, -pitch
	gcopy 2, 0, 0, 1600, 600
	return

*proc_reticle
	if cammode = 0{
		//n3d_setCameraPosition -48.0, 0.0 - x, -8.0 + z
		//n3d_setpolygon 4096 + cnt, $FF0000, 0.0, -x - 0.25, z,   0.0, -x + 0.25, z,   256.0, -x, z
	}
	return

*proc_title_viewship
	vs_angle = vs_angle + 3
	n3d_setpolygon 1536, $444444, -10.0, -10.0, 2.0,	-10.0, 10.0, 2.0,	 8.0, -10.0, 2.0
	n3d_setpolygon 1536, $444444,   8.0,  10.0, 2.0,	-10.0, 10.0, 2.0,	 8.0, -10.0, 2.0
	n3dm_setModel testplane, 1, 0, 0, 0
	n3d_setCameraPosition cos(deg2rad(vs_angle)) * 24.0, sin(deg2rad(vs_angle)) * 24.0, -12.0
	n3d_setCameraAngleByLookingAt 0.0, 0.0, 0.0
	return


*proc_titletext
	taglab_add 320, 180, "SCRAMBLE BLAZE", 1
	if camtime \ 96 < 32{
		taglab_add 320, 240, "[Btn A] でスタート", 1
	}else{
		taglab_add 320, 240, "操作方法", 1
		taglab_add 320, 280, "[←][↑][→][↓]で移動", 1
		taglab_add 320, 320, "[Btn A]で発射", 1
		taglab_add 320, 360, "[Btn L]で視点変更", 1
	}
	taglab_add 320, 440, "First Prototype", 1
	taglab_add 320, 460, "(C)2023 nasTTY", 1
	return

*title_control
	if KY_Z{
		camtime = 0
		cammode = 1
	}
	return